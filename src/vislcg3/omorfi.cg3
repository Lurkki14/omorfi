# omorfi.cg3
# Constraint grammar for Finnish
# GNU GPLv3
# Authors: Tommi A Pirinen
# but also remade based on Fred Karlsson's fincg:

##! A note on comments: ##! is used for testing:
##! euro sign for example of remove not applying
##! dollar sign for remove rule firing

##  This program is free software: you can redistribute it and/or modify
##  it under the terms of the GNU General Public License as published by
##  the Free Software Foundation, either version 3 of the License.
##
##  This program is distributed in the hope that it will be useful,
##  but WITHOUT ANY WARRANTY; without even the implied warranty of
##  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
##  GNU General Public License for more details.
##
##  You should have received a copy of the GNU General Public License
##  along with this program.  If not, see <http://www.gnu.org/licenses/>.

# {{{ options
STRICT-TAGS += NOUN VERB AUX ADJ ADV ADP PROPN PRON SYM PUNCT CCONJ SCONJ INTJ NUM
    DET
    NOM PAR ACC GEN INE ELA ILL ADE ABL ALL TRA INS ABE LAT ESS
    SG PL
    ACTV PASV INDV POTN IMPV COND PRES PAST
    INFA INFE INFMA INFMINEN
    POSSSG1 POSSSG2 POSS3 POSSPL1 POSSPL2
    CLITHAN CLITKO CLITPA CLITS CLITKA CLITKAAN CLITKIN
    ORD CARD
    NEG CONNEG
    PCPNUT PCPVA PCPMA
    SG1 SG2 SG3 PL1 PL2 PL3 SG0
    POS CMP SUP
    PRS DEM
    <DRV_LA> <DRV_VA> <DRV_NUT> <DRV_TU> <DRV_MA> <DRV_U> <DRV_MINEN>
    <DRV_MA> <DRV_JA> <DRV_NEN> <DRV_STI>
    <LEX_STI>
    <POSITION_FINAL> <POSITION_INITIAL>
    <STYLE_DIALECTAL> <STYLE_ARCHAIC> <STYLE_NONSTANDARD>
    <*lowercased> <*UPPERCASED> <*Titlecased> <*dETITLECASED>
    <**PROPN-BLOCKING> ;
OPTIONS += no-inline-sets ;

DELIMITERS = "<.>" "<!>" "<?>" ;

# }}}

SETS

# {{{ grammar groups
## Nominal poses
LIST NOMINAL = NOUN ADJ PRON PROPN ;
## Clitic particle tags
LIST CLITIC = CLITHAN CLITKO CLITPA CLITS CLITKA CLITKAAN CLITKIN ;
## Conjunctions
LIST CONJUNCTION = CCONJ SCONJ ;
## any vebs
LIST VERBAUX = VERB AUX ;
## copula?
LIST COPULA = ("olla" AUX) ;
##
LIST VERB-PL3 = (VERB PL3) (AUX PL3) ;
## ad-adjectives etc.
LIST ADA = ADV ;
## AD words
LIST AD = ADJ ADV ADP ;
## comparatives and superlatives
LIST CMP-SUP = CMP SUP ;
##
LIST POSS = POSSSG1 POSSSG2 POSS3 POSSPL1 POSSPL2 ;
## case subgroups
LIST LOCATIVE-IN = INE ADE ;
LIST LOCATIVE-TO = ILL ALL ;
LIST LOCATIVE-FROM = ELA ABL ;
LIST ILL/ALL/PAR = ILL ALL PAR ;
LIST PERS-PRON-SG = (PRS SG1) (PRS SG2) (PRS SG3) ;
# }}}
# {{{ sentences and clauses
LIST BEGINSENT = >>> ;
LIST ENDSENT = "<.>" "<!>" "<?>" ;
LIST CLB = "<,>" ;
# }}}
# {{{ words

## short words that many find problems with
LIST BADEVIL_WORDS =
    ("kui" NOUN) ("tai" NOUN) ("vai" NOUN) ("pai" NOUN)
    ("par" NOUN) ("tule" NOUN) ("kuvi" NOUN) ("kos" NOUN)
    ("mon" NOUN) ("lev" NOUN)
    ("taka" NOUN) ("edus" NOUN)
    ("he" INTJ) ;

## propns that overlap commoner words
LIST RARER-PROPN =
    ("Vanha" PROPN) ("Mei" PROPN) ("Osa" PROPN) ("Hanska" PROPN)
    ("Osta" PROPN) ("Leikkaa" PROPN) ("Varo" PROPN) ("Tarkki" PROPN)
    ("Onkki" PROPN) ("Ede" PROPN) ("Mietti" PROPN) ("Metsä" PROPN)
    ("Puu" PROPN) ("Koto" PROPN) ("Mörä" PROPN) ("Tuli" PROPN)
    ("Hauska" PROPN) ("Uutinen" PROPN) ("Piha" PROPN) ("Sakko" PROPN)
    ("Ota" PROPN) ("Leikkaa" PROPN) ("Lähdet" PROPN) ("Matka" PROPN)
    ("Lähellä" PROPN) ("Makkara" PROPN) ("Maku" PROPN) ("Suo" PROPN)
    ("Kauha" PROPN) ("Tietty" PROPN) ("Muu" PROPN) ("Sen" PROPN)
    ("Erinomainen" PROPN) ("Allekirjoittanut" PROPN) ("Markkina" PROPN)
    ("Turu" PROPN) ("Muu" PROPN) ("Alku" PROPN) ("Lava" PROPN)
    ("Tila" PROPN) ("Pyhä" PROPN) ("Suora" PROPN) ("He" PROPN)
    ("Sen" PROPN) ("Ainoa" PROPN) ("Viimeinen" PROPN) ("Ole" PROPN)
    ("Jo" PROPN) ("Ole" PROPN) ("Ihana" PROPN) ("Tos" PROPN)
    ("Mies" PROPN) ("As" PROPN) ("Asi" PROPN) ("Viimeinen" PROPN)
    ("Toivo" PROPN) ("Pitkä" PROPN) ("Viimeinen" PROPN)
    ("Lusikka" PROPN) ("Asema" PROPN) ("Mies" PROPN)
    ("Olin" PROPN) ("Tieto" PROPN) ("Erä" PROPN) ("Jyrkänne" PROPN)
    ("Kello" PROPN) ("Kuppi" PROPN) ("Mutta" PROPN) ("Yhtiö" PROPN)
    ("Ka" PROPN) ("Kutsu" PROPN) ("Teos" PROPN) ("Teema" PROPN)
    ("Puute" PROPN) ("Soi" PROPN) ("Hakkuu" PROPN) ("Tu" PROPN)
    ("Vä" PROPN) ("Vähä" PROPN) ("Karta" PROPN) ("Silta" PROPN) 
    ("Onni" PROPN) ("Joki" PROPN) ("Muuri" PROPN) ("Vuo" PROPN)
    ("Vuoksi" PROPN) ("Kiva" PROPN) ("Miele" PROPN) 
    ("Maakunta" PROPN) ("Pia" PROPN) ("Per" PROPN) ("Olle" PROPN)
    ("Johan" PROPN) ("Aino" PROPN) ("Halunen" PROPN) ;
## also some cases where a rare noun blocks commoner propn
## often languages, nationalities, tribes
LIST RARER-NOUN = ("missouri" NOUN) ("tai" NOUN) ("vai" NOUN) 
    ("muutti" NOUN) ;
## adverbs that are rarer than adp or noun counterparts. also some
## questionable advs that should've been intjs
LIST RARER-ADV =
    ("kuin" ADV) ("kun" ADV) ("tai" ADV) ("sen" ADV) ;
## verbs that shadow some noun forms etc.
LIST RARER-VERB =
    ("juuria" VERB) ("eritä" VERB) ("niittää" VERB)
    ("nytkiä" VERB) ("siitä" VERB) ("siittää" VERB)
    ("suomia" VERB) ("suoria" VERB) ("taata" VERB)
    ("vastoa" VERB) ("oieta" VERB) ("niistää" VERB) ;
## adverbs that are much commoner than nominal counterparts
LIST DEFAULT-ADV =
    ("erikseen" ADV) ("erilleen" ADV) ("erillään" ADV) 
    ("lainkaan" ADV) ("läpeensä" ADV) ("kaikkiaan" ADV)
    ("kumminkaan" ADV) ("kumminkin" ADV) ("naimisiin" ADV)
    ("naimisissa" ADV) ("jäljelle" ADV) ("kerrassaan" ADV)
    ("kotoisin" ADV) ("kumminkin" ADV) ("kumminkaan" ADV)
    ("oikeastaan" ADV) ("parhaimmillaan" ADV) ("pelkästään" ADV)
    ("tietenkin" ADV) ("tietenkään" ADV) ("toisinaan" ADV)
    ("tosiaan" ADV) ("usein" ADV) ("vahnastaan" ADV)
    ("vähintään" ADV) ("yhteensä" ADV) ("yhtään" ADV)
    ("yksinään" ADV) ("yksistään" ADV) ("öisin" ADV)
    ("varsin" ADV) ;
## following adverbs are quite safe to select umless matching case is in
## ±1 tokens (depending if ADJ or NOUN ambiguity)
## inessives np initial
LIST ADV-LIKE-ADJ-INE =
    ("yhdessä" ADV) ("vinossa" ADV) ("pystyssä" ADV) ;
## illatives np initial
LIST ADV-LIKE-ADJ-ILL =
    ("suoraan" ADV) ("yksiin" ADV) ("myöhään" ADV)
    ("turhaan" ADV) ("varmaan" ADV) ("täyteen" ADV) 
    ("pitkään" ADV) ("pystyyn" ADV) ;
LIST ADV-LIKE-ADJ-ALL =
    ("uudelleen" ADV) ;
LIST ADV-LIKE-ADJ-ELA =
    ("uudestaan" ADV) ;
LIST ADV-LIKE-ADJ-ADE =
    ("syvällä" ADV) ("samalla" ADV) ("pitkällä" ADV) ;
LIST ADV-LIKE-ADJ-INS =
    ("myöhemmin" ADV) ("nykyisin" ADV) ;
LIST ADV-LIKE-ADJ-GEN = ("vähän" ADV) ("paljon" ADV) 
    ("vähemmän" ADV) ("enemmän" ADV) ;
## translatives np final
## instructives ... maybe
LIST ADV-LIKE-NOUN-TRA =
    ("tarpeeksi" ADV) ("käsiksi" ADV) ("mieliksi" ADV) ("osaksi" ADV)
    ("periksi" ADV) ;
## instructives ... maybe
LIST ADV-LIKE-NOUN-INS =
    ("paikoin" ADV) ("selin" ADV) ("silloin" ADV)
    ("suinkin" ADV) ("suinkaan" ADV) ("tuskin" ADV) 
    ("vihdoin" ADV) ("puolin" ADV) ("muutoin" ADV) ;
## adessives
LIST ADV-LIKE-NOUN-ADE =
    ("todella" ADV) ("tavallaan" ADV) ("mielellään" ADV)
    ("perillä" ADV) ("suunnitteilla" ADV) ("vireillä" ADV) 
    ("jäljellä" ADV) ;
LIST ADV-LIKE-NOUN-ALL =
    ("perille" ADV) ("suunnilleen" ADV) ;
LIST ADV-LIKE-NOUN-ELA =
    ("suorastaan" ADV) ;
LIST ADV-LIKE-NOUN-INE =
    ("jumissa" ADV) ;
## adps that are rarer than adv or noun counterparts
## prefer ADPS-WITH-CASE over RARER-ADP
LIST RARER-ADP =
    ("silloin" ADP) ("vaikka" ADP) ("aina" ADP) ;
## Adpositions by their complement
## adpositions with partitive complement
LIST ADPS-WITH-PAR = ("ylös" ADP) ("päin" ADP) ("vastaan" ADP) 
    ("ilman" ADP) ("kautta" ADP) ("huolimatta" ADP) ("asti" ADP) 
    ("sitten" ADP) ;
## adositions with genitive complement
LIST ADPS-WITH-GEN = ("takana" ADP) ("mukaan" ADP) ("vuoksi" ADP) 
    ("alla" ADP) ("kanssa" ADP) ("kautta" ADP) ("puolesta" ADP) 
    ("läpi" ADP) ("johdolla" ADP) ("sisällä" ADP) ("perusteella" ADP) 
    ("sijaan" ADP) ("suhteen" ADP) ("avulla" ADP) ("jälkeen" ADP) ;
## adpositions with illative complement
LIST ADPS-WITH-ILL = ("nähden" ADP) ;
## adpositions with illative complement
LIST ADPS-WITH-ELA = ("ulos" ADP) ;
## Verbs with inf complements
## ... infa
LIST VERBS-WITH-INFA = ("alkaa" AUX) ("haluta" AUX) ("tahtoa" AUX) 
    ("saattaa" AUX) ("aikoa" AUX) ;
## ... inf maan
LIST VERBS-WITH-INFMAAN = ("alkaa" AUX) ("päästä" AUX) ("ruveta" AUX) 
    ("pystyä" VERB) ("joutua" AUX) ;
## nouns where plural is rare / theoretical / shadows more common stuff
LIST NOUNS-RARELY-PLURAL = ("kota" NOUN PL) ("ikä" NOUN PL)
    ("kirjo" NOUN PL) ("lei" NOUN PL) ("bi" NOUN PL)
    ("noki" NOUN PL) ("tau" NOUN PL) ("show" NOUN PL)
    ("häkä" NOUN PL) ("kulku" NOUN PL) ("koko" NOUN PL) ;
## numerals that are used nouns too
LIST NUM-MULTIPLIER = ("puoli" NUM) ("sata" NUM) ("miljoona" NUM) 
    ("pari" NUM) ;
## temporal adverbs that can be noun forms
LIST TIME-ADV = ("maanantaina" ADV) ("tiistaina" ADV) ("keskiviikkona" ADV)
    ("torstaina" ADV) ("perjantaina" ADV) ("lauantaina" ADV)
    ("sunnuntaina" ADV) ;
## curse words that go after voi
LIST VITTUSAATANA = ("vittu" INTJ) ("saatana" INTJ)
    ("hitto" INTJ) ("perkele" INTJ) ("!" PUNCT) ;
## the words where 2nd person singular possessives overlap with e.g. past
## 3rd person singular e.g. "voisi" = {your butter(s), he could},
## "tiesi" = {your roads, he knew} but also: "kuusi" = {your moon(s),
## six}
LIST SG2POSS-CONFUSION = ("kuu" POSSSG2) ("voi" POSSSG2) ("tie" POSSSG2)
    ("tunti" POSSSG2) ("lupa" POSSSG2) ("rupi" POSSSG2) ("hai" POSSSG2)
    ("taju" POSSSG2) ("pyy" POSSSG2) ("kerä" POSSSG2) ("vasta" POSSSG2)
    ("pää" POSSSG2) ("harja" POSSSG2) ("vuo" POSSSG2) ("pää" POSSSG2)
    ("halu" POSSSG2) ("tapa" POSSSG2) ("tosi" POSSSG2) ("pola" POSSSG2 ILL)
    ("poli" POSSSG2 ILL);
## "siitä" = {to conceive, from it}
LIST SIITTÄÄ-VERBS = ("siittää" VERB) ("siitä" VERB) ("si’itä" VERB) ;
LIST SIITÄ-DEM = ("se" PRON) ("se" DET) ;
## "sinun" = yours
LIST SINUN = ("sinä" SG GEN) ;
## ja ADV is spoken
LIST JA-ADV = ("ja" ADV) ;
## van Houten, etc.
LIST VAN-PROPN = ("van" PROPN) ;
## Helsingin = {Helsinki’s, Helsing’s}
LIST HELSING-PROPN = ("Helsing" PROPN) ;
## spoken
LIST ETTÄ-INTJ = ("että" INTJ) ;
## sekä - että is paired cconj, otherwise että sconj
LIST SEKÄ-CCONJ = ("sekä" CCONJ) ;
LIST ETTÄ-CCONJ = ("että" CCONJ) ;
## niin - kuin is paired cconj
LIST NIIN-CCONJ = ("niin" CCONJ) ;
LIST KUIN-CCONJ = ("kuin" CCONJ) ;
## niiden {niisi’s, theirs} (I dunno what niisi is)
LIST NIISI-NOUN = ("niisi" NOUN) ;
## ensin {first, some odd verb that doesn't really go with 1sg}
LIST ENTÄÄ-VERB = ("entää" VERB) ;
LIST ENSIN-ADV = ("ensin" ADV) ;
## nainen {female, I might marry/fuck}
LIST NAINEN-VERB = ("naida" VERB POTN) ;
## vähän = a little either adv or adj gen
LIST VÄHÄN-ADV = ("vähän" ADV) ;
LIST VÄHÄN-ADJ = ("vähä" ADJ GEN) ;
## vaikka mitä
LIST VAIKKA-ADV = ("vaikka" ADV) ;
LIST MIKÄ-PRON = ("mikä" PRON) ;
## häntä {tail, him}
LIST HÄNTÄ-NOUN = ("häntä" NOUN) ;
LIST HÄNTÄ-PRON = ("hän" PRON PAR) ;
## erikseen { separately, with his rounds }
LIST ERIKSEEN-ADV = ("erikseen" ADV) ;
LIST ERIKSEEN-NOUN = ("erä" NOUN PL POSS3) ;
## jälkeen { after, into trace }
LIST JÄLKEEN-NOUN = ("jälki" NOUN ILL) ;
LIST JÄLKEEN-AD = ("jälkeen" ADV) ("jälkeen" ADP) ;
# jäljelle { left, onto trace }
LIST JÄLJELLE-NOUN = ("jälki" NOUN ALL) ;
LIST JÄLJELLE-ADV = ("jälkeen" ADV) ;
## uusia = { new, to renew }
LIST UUSI-ADJ = ("uusi" ADJ) ;
LIST UUSI-VERB = ("uusia" VERB) ;
## Japan is some rarer propn according to ...
LIST JAPANI-PROPN = ("Japani" PROPN) ;
LIST JAPAN-PROPN = ("Japan" PROPN) ;
## ajan { I drive, time, during }
LIST AJAN-NOUN = ("aika" NOUN GEN) ;
LIST AJAN-AD = ("ajan" ADV) ("ajan" ADP) ;
## koko = whole
LIST KOKO-ADJ = ("koko" ADJ) ;
## uudestaan = {again, from their new}
LIST UUDESTAAN-ADJ = ("uusi" ADJ ELA POSS3) ;
## jäljelle = {left, to a trace}
LIST JÄLJELLE-AD = ("jäljelle" ADV) ("jäljelle" ADP) ;
## ollenkaan = {at all, while being}
LIST OLLENKAAN-ADV = ("ollenkaan" ADV) ;
LIST OLLENKAAN-VERB = ("olla" INFE CLITKAAN) ;
## erään = {of some, to a set}
LIST ERÄÄN-PRON = ("eräs" PRON SG GEN) ;
## merkitystä = {of importance, from marked}
LIST MERKITYSTÄ-PAR = ("merkitys" PAR) ;
## noissa = {in those, in ashes}
LIST NOISSA-PRON = ("nuo" PRON INE) ("tuo" PRON PL INE) ;
LIST NOISSA-NOUN = ("noki" SG INE) ;
## peräisin = {originating from, I'd guarantee}
LIST PERÄISIN-ADV = ("peräisin" ADV) ;
LIST PERÄISIN-VERB = ("perätä" VERB COND SG1) ;
## sekin = {that one too, of cheque}
LIST SEKIN-PRON = ("se" PRON CLITKIN) ;
LIST LUNASTAA-VERB = ("lunastaa" VERB) ;
## 
LIST TUO-PRON = ("tuo" PRON) ;
LIST KAIKKI = ("kaikki" PRON) ("kaikki" ADJ) ;
## vastaan {against}
LIST VASTAAN-ADV = ("vastaan" ADV) ;
##
LIST ARVOINEN-ADJ = ("arvoinen" ADJ) ;
LIST KALLIS-ADJ = ("kallis" ADJ) ;
## pieniä = {make smaller, small}
LIST PIENIÄ-ADJ = ("pieni" ADJ PAR) ;
## vaaleissa etc. {elections, blondes}
LIST VAALEISSA-ADJ = ("vaalea" ADJ PL INE) ;
LIST VAALEISTA-ADJ = ("vaalea" ADJ PL ELA) ;
LIST VAALEIHIN-ADJ = ("vaalea" ADJ PL ILL) ;
LIST VAALEILLA-ADJ = ("vaalea" ADJ PL ADE) ;
## 
LIST OTTAA-VERB = ("ottaa" VERB) ;
##
LIST KALLIS-NOUN = ("kallis" NOUN) ;
##
LIST HYVIN-ADA = ("hyvin" ADV) ;
LIST LIIAN-ADV = ("liian" ADV) ;
##
LIST MITÄ-ADV = ("mitä" ADV) ;
##
LIST TÄYSIN-ADA = ("täysin" ADV) ;
##
LIST EDELLÄ-ADV = ("edellä" ADV) ;
##
LIST ENNEN-ADV = ("ennen" ADV) ;
##
LIST IKINÄ-ADV = ("ikinä" ADV) ;
##
LIST KAHDEN-ADV = ("kahden" ADV) ;
##
LIST KOOLLE-ADV = ("koolle" ADV) ;
##
LIST KAUPPA-NOUN = ("kauppa" NOUN) ;
##
LIST KOHTA-ADV = ("kohta" ADV) ;
##
LIST KUTSUA-VERB = ("kutsua" VERB) ;
##
LIST LISÄÄ-ADV = ("lisää" ADV) ;
##
LIST JOS-ADV = ("jos" ADV) ;
##
LIST KAUEMMAKSI-ADV = ("kauemmaksi" ADV) ;
LIST KAUEMPAA-ADV = ("kauempaa" ADV) ;
LIST KAUEMPANA-ADV = ("kauempana" ADV) ;
LIST KAUEMMAKSI-ADJ = ("kauempi" ADJ TRA) ;
LIST KAUEMPAA-ADJ = ("kauempi" ADJ TRA) ;
LIST KAUEMPANA-ADJ = ("kauempi" ADJ ESS) ;
LIST LÄHEMMÄKSI-ADV = ("lähemmäksi" ADV) ;
LIST LÄHEMPÄÄ-ADV = ("lähempää" ADV) ;
LIST LÄHEMPÄNÄ-ADV = ("lähempänä" ADV) ;
LIST LÄHEMMÄKSI-ADJ = ("lähempi" ADJ TRA) ;
LIST LÄHEMPÄÄ-ADJ = ("lähempi" ADJ PAR) ;
LIST LÄHEMPÄNÄ-ADJ = ("lähempi" ADJ ESS) ;
## näin = {so, I saw}
LIST NÄIN-ADV = ("näin" ADV) ;
LIST NÄIN-VERB = ("nähdä" VERB SG1 PAST) ;
##
LIST NIIN-ADV = ("niin" ADV) ;
##
LIST ESIM = ("esim." NOUN) ("esimerkiks" ADV) ("esimerkki" NOUN SG TRA) ;
##
LIST TAI-CCONJ = ("tai" CCONJ) ;
## 
LIST NÄKÖJÄÄN-ADV = ("näköjään" ADV) ;
##
LIST SIKSI-ADV = ("siksi" ADV) ;
## 
LIST SUOTTA-ADV = ("suotta" ADV) ;
## 
LIST SYNTYISIN-ADV = ("syntyisin" ADV) ;
##
LIST TAKAISIN-ADV = ("takaisin" ADV) ;
## 
LIST TOISTAMISEEN-ADV = ("toistamiseen" ADV) ;
##
LIST MINÄ-PRON = ("minä" PRON) ;
##
LIST HEIDÄN-PRON = ("he" PRON GEN) ("hän" PRON PL GEN) ;
##
LIST LUJAA-ADV = ("lujaa" ADV) ;
##
LIST SUORAAN-ADV = ("suoraan" ADV) ;
##
LIST KAPINA-NOM = ("kapina" NOUN NOM) ;
## kotia = {home, lappic tent}
LIST KOTA-NOUN = ("kota" NOUN) ;
LIST KOTI-NOUN = ("koti" NOUN) ;
## kuolema = {death, die-er}
LIST KUOLEMA-VERB = ("kuolla" VERB PCPMA) ("kuolla" VERB <DRV_MA>) ;
LIST KUOLEMA-NOUN = ("kuolema" NOUN) ;
## olkaa = {shoulder, be imperative (be quiet!)}
LIST OLKAA-NOUN = ("olka" NOUN SG PAR) ;
LIST OLKAA-VERB = ("olla" VERB IMPV PL2) ;
## teillä = {teas, roads, yours}
LIST TEE-PLURAL = ("tee" NOUN PL) ;
LIST TIE-PLURAL = ("tie" NOUN PL) ;
LIST TE-PLURAL = ("te" PRON) ("sinä" PRON PL) ;
## tilanne = {your space, situation}
LIST TILANNE-NOUN = ("tilanne" NOUN) ;
LIST TILANNE-POSS = ("tila" NOUN POSSPL2) ;
## vanhempi = {elder (parent), older}
LIST VANHEMPI-NOUN = ("vanhempi" NOUN) ;
## hiukan = [a little, a dustknot's}
LIST HIUKAN-NOUN = ("hiukka" NOUN SG GEN) ;
## mm = {millimetres, world championship, 2000, among others, a-ha}
LIST MM-NOUN = ("mm" NOUN) ;
## lähtien = {from onwards, by going}
LIST LÄHTIEN-ADP = ("lähtien" ADP) ;
##
LIST PÄIN-ADP = ("päin" ADP) ;
## omaa = {own verb / det}
LIST OMAAN-VERB = ("omata" VERB SG1) ;
LIST OMAAN-ADJ = ("oma" ADJ ILL) ;
LIST OMAA-VERB = ("omata" VERB SG2) ;
LIST OMAA-ADJ = ("oma" ADJ PAR) ;
## voi = {butter, oh!, can, could}
LIST VOI-INTJ = ("voi" INTJ) ;
LIST VOI-AUX = ("voida" AUX SG3) ;
## ei = {no, not, none, negative aux, }
LIST EI-INTJ = ("ei" INTJ) ;
LIST EI-AUX = ("ei" AUX) ;
## tuktittava 
LIST TUTKITTAVA-DRV-VA = ("tutkittaa" <DRV_VA>) ;
LIST TUTKITTAVA-NOUN = ("tutkittava" NOUN) ;
## copulas for minen verb chains
LIST OLE-CONNEG = ("olla" VERB CONNEG) ;
## vaan = {not ... but (CCONJ), not really at all (ADV)}
LIST VAAN-ADV = ("vaan" ADV) ;
LIST VAAN-CCONJ = ("vaan" CCONJ) ;
# }}}
# {{{ secondaries
## we have these tags when analyser doesn't match the case
LIST RECASED = <*lowercased> <*UPPERCASED> <*Titlecased> <*dETITLECASED> ;
## we have some blacklists
LIST BLACKLISTED = <**PROPN-BLOCKING> ;
## generic weight = approximate log prob of some linear combo
LIST MIN-WEIGHT = <W=MIN> ;
## we count compound boundaries
LIST MIN-COMPOUND = <CMP=MIN> ;
## tags for known wrong or odd stuff
LIST NONSTANDARDS = <STYLE_ARCHAIC> <STYLE_DIALECTAL> <STYLE_NONSTANDARD> ;
## derivations (apart from the near-inflectional)
LIST DERIVATIONS = <DRV_LA> <DRV_U> <DRV_MINEN> <DRV_JA> <DRV_NEN>
    <DRV_ISA> <DRV_TAVA> <DRV_STI> ;

# }}}

BEFORE-SECTIONS # {{{ preprocess worst stuff away

REMOVE:bad-words BADEVIL_WORDS ;

# }}}

SECTION # {{{ words minimal context

# UD preferences

# FIXME: use agreements to match lemma
REMOVE:UD-ordinal-is-adjective NUM (0 ORD) ;

# trad. possessives
# sinun {kuusi, voisi, tiesi}
REMOVE:only-poss-if-pron-sg2 SG2POSS-CONFUSION (NOT -1 SINUN) ;

# surface-form specific disambiguation between few lexemes

##! € ollaan menos sinne ja.
REMOVE:ja-adv JA-ADV (NOT 1 ENDSENT) ;

##! € ai että, voi että.
REMOVE:että-intj ETTÄ-INTJ (NOT -1 INTJ) ;
##! € oli siellä sekä kaljaa että apinoita.
REMOVE:sekä-että-cconj ETTÄ-CCONJ (NOT -1* SEKÄ-CCONJ) ;
##! € oli siellä niin kaljaa kuin apinoitakin.
REMOVE:niin-kuin-cconj NIIN-CCONJ (NOT 1* KUIN-CCONJ) ;

##! $ Helsingissä on lunta.
##! € näitkö sen uuden van Helsingin episodin?
REMOVE:Helsing-needs-van HELSING-PROPN (NOT -1 VAN-PROPN) ;

##! $ pulloissa on vanhan näköiset etiketit.
REMOVE:van-needs-PROPN VAN-PROPN (NOT 1 PROPN) ;

##! $ Japaniin suuntautuva vierailu.
REMOVE:Japan-not-country JAPAN-PROPN (0 JAPANI-PROPN) ;

##! $ nainen on kuin morsian.
REMOVE:nainen-unlikely-potential  NAINEN-VERB (0 NOUN) ;

##! € kun rahaa ei ole, löytää vaikka mitä.
##! $ vaikka rahaa ei ole, löytää sitä.
REMOVE:vaikka-mitä VAIKKA-ADV (NEGATE 1 MIKÄ-PRON) ;

##! $ niiden pitää tulla myös.
REMOVE:niisi-niiden NIISI-NOUN (0 PRON) ;

##! $ siitä en tiedä mitään.
##! € siitä maailma täyteen lapsia.
REMOVE:siittää-siitä SIITTÄÄ-VERBS (0 SIITÄ-DEM) ;

##! $ ensin syödään sitten vasta mennään.
REMOVE:entää-ensin ENTÄÄ-VERB (0 ENSIN-ADV) ;


##! $ he odottivat häntä.
##! € koira juoksi häntä heiluen.
SELECT:häntä-pron HÄNTÄ-PRON (0 HÄNTÄ-NOUN) ;

##! $ kaupunginvaltuusto hyväksyi liitoksen maanantaina.
SELECT:temprals-prefer-adv TIME-ADV (0 NOUN) ;

##! $ päätetään siitä erikseen.
SELECT:erikseen-adv ERIKSEEN-ADV (0 ERIKSEEN-NOUN);

##! $ aamun jälkeen on yö.
REMOVE:jälkeen-ad JÄLKEEN-NOUN (0 JÄLKEEN-AD) ;
REMOVE:jäljelle-adv JÄLJELLE-NOUN (0 JÄLJELLE-AD) ;

##! $ Intian uusi pääministeri on Narendra Modi.
SELECT:uusi-adj UUSI-ADJ (0 UUSI-VERB) ;

##! $ hän ei ole ollenkaan rehellinen.
SELECT:ollenkaan-adv OLLENKAAN-ADV (0 OLLENKAAN-VERB) ;

##! $ oikeus omaan kehoon
##! $ minä omaan kehoni
SELECT:omaan-adj-ill OMAAN-ADJ (1 ILL) ;
##! $ pitää perustella omaa olemassaoloaan
SELECT:omaa-adj-intj OMAA-ADJ (1 PAR) ;
##! $ voi vittu saatana!
REMOVE:voi-intj-intj VOI-INTJ (NEGATE 1 VITTUSAATANA) ;
##! $ lääkäri voi määrätä lääkkeitä
SELECT:voi-aux-infa VOI-AUX (1* INFA BARRIER VERB) ;
##! $ ei vittu saatana!
REMOVE:ei-intj-intj EI-INTJ (NEGATE 1 VITTUSAATANA) ;
##! $ tutkittavan todellisuudentaju näyttäytyy
REMOVE:tutkittava-over-tutkittaa TUTKITTAVA-DRV-VA (0 TUTKITTAVA-NOUN) ;
##! $ se ei vaan onnistu
SELECT:ei-vaan-adv VAAN-ADV (-1 NEG) ;
##! $ ulkopolitiikka ei tehdä pressan johdolla, vaan pressasta huolimatta
SELECT:ei-blah-vaan-cconj VAAN-CCONJ (-2* NEG BARRIER BEGINSENT) ;
# }}}
SECTION # {{{ FGK-word-specific
# I have only selected the ones I can explain and fidn examples of:
# I also skipped:
# * adps that already have case bindings
# * and advs too
# * nouns that have generic poss solution (kuusi, voisi, ...)
# ––Flammie

##! $ emme ratkaise pitkän ajan ongelmia.
SELECT:ajan-noun AJAN-NOUN (-1 GEN) ;
##! $ auringon pinta muuttuu koko ajan.
SELECT:ajan-noun AJAN-NOUN (-1 KOKO-ADJ) ;

##! $ erään sateen jälkeen olin yksin.
##! € toiseen erään vaihtopelaajien määrää vähennettiin.
SELECT:erään-pron ERÄÄN-PRON (1 GEN) ;




##! $ sillä on merkitystä elinkeinoelämässä.
##! € salasanan löydät seinälle merkitystä tekstistä.
SELECT:merkitystä-par MERKITYSTÄ-PAR (NOT 1 ELA) ;

##! $ aiotko mennä sinne noissa vaatteissa.
SELECT:noissa-pron NOISSA-PRON (0 NOISSA-NOUN) ;

##! $ mistä se on peräisin.
SELECT:peräisin-adv PERÄISIN-ADV (0 PERÄISIN-VERB) (0* COPULA) ;

##! $ sekin on vielä.
##! € lunastan sen sekin jahka joudan.
SELECT:sekin-pron SEKIN-PRON (NEGATE -1* LUNASTAA-VERB BARRIER VERB) ;


##! hmm?
SELECT:kaikki-tuo TUO-PRON (-1 KAIKKI) (NOT -2 PRON) ;

##! $ tankki täyteen bensaa.

##! $ ota vastaan se lahja.
SELECT:ottaa-vastaan-adv VASTAAN-ADV (-1* OTTAA-VERB BARRIER VERB) ;

##! $ minkä arvoinen turkis se oli.
SELECT:gen-arvoinen ARVOINEN-ADJ (-1 GEN) ;

##! $ Japani on kallis maa.
SELECT:kallis-adj KALLIS-ADJ (0 KALLIS-NOUN) ;


##! $ pieniä sieviä sieniä.
SELECT:pieniä-adj PIENIÄ-ADJ (1 PAR) ;


##! € presidentti Trump voitti vaaleissa.
##! $ vaaleissa munkeissa on enemmän makua.
REMOVE:vaaleissa-adj VAALEISSA-ADJ (NEGATE 1 INE) ;

##! $ pidän vaaleista naisista.
REMOVE:vaaleista-adj VAALEISTA-ADJ (NEGATE 1 ELA) ;

##! € presidentti Trump toi vaaleihin markkinoiden tuntua.
REMOVE:vaaleihin-adj VAALEIHIN-ADJ (NEGATE 1 ILL) ;

##! € presidentti Trump valittiin demokraattisilla vaaleilla.
REMOVE:vaaleilla-adj VAALEILLA-ADJ (NEGATE 1 ADE) ;

##! € se on hyvin suuri.
##! € se meni hyvin nopeasti.
SELECT:hyvin-adv HYVIN-ADA (1 AD) ;

##! € se on liian vanha.
##! $ vim poistaa liian lian.
SELECT:liian-adv LIIAN-ADV (NEGATE 1 GEN) ;

##! € mitä suurempi peukalo sitä suurempi pikkurilli.
##! € mitä suurimmissa määrin kyllä.
SELECT:mitä-ada MITÄ-ADV (1 CMP-SUP) ;

##! $ hän on täysin kyvytön.
SELECT:täysin-ada TÄYSIN-ADA (1 AD) ;


##! $ he ovat vielä edellä.
##! € kuningas ei edellä jätkää.
##! € kuka on sinua edellä?
SELECT:edellä-adv EDELLÄ-ADV (NEGATE -1* NEG) (NEGATE -1 GEN) (NEGATE -1 PAR) (NEGATE 1 PAR) ;

##! $
SELECT:ennen-adv-kuin ENNEN-ADV (1 KUIN-CCONJ) ;

##

##! € en olis ikinä uskonu.
SELECT:ikinä-adv IKINÄ-ADV (NEGATE -1 ESS) ;

##! € miten paljon aikaa on jäljellä.

##!
SELECT:kahden-adv KAHDEN-ADV (NEGATE 1 GEN) (NEGATE 1 KAUPPA-NOUN) ;



##!
SELECT:jos-kohta-adv KOHTA-ADV (-1 JOS-ADV) ;

##! kutsutaan kokous koolle.
SELECT:kutsua-koolle KOOLLE-ADV (-1* KUTSUA-VERB) ;


##

SELECT:lisää-adv LISÄÄ-ADV (0* PAR BARRIER CLB) ;

##!
SELECT:kauemmaksi-adv KAUEMMAKSI-ADV (0 KAUEMMAKSI-ADJ) (NEGATE 1 TRA) ;
SELECT:kauempaa-adv KAUEMPAA-ADV (0 KAUEMPAA-ADJ) (NEGATE -1 PAR) ;
SELECT:kauempana-adv KAUEMPANA-ADV (0 KAUEMPANA-ADJ) (NEGATE -1 ESS) ;
SELECT:lähemmäksi-adv LÄHEMMÄKSI-ADV (0 LÄHEMMÄKSI-ADJ) (NEGATE 1 TRA) ;
SELECT:lähempää-adv LÄHEMPÄÄ-ADV (0 LÄHEMPÄÄ-ADJ) (NEGATE 1 PAR) ;
SELECT:lähempänä-adv LÄHEMPÄNÄ-ADV (0 LÄHEMPÄNÄ-ADJ) (NEGATE 1 ESS) ;






SELECT:niin-tai-näin-adv NÄIN-ADV (-2 NIIN-ADV) (-1 TAI-CCONJ) ;
SELECT:esim-näin-adv NÄIN-ADV (-1 ESIM) ;

SELECT:näköjään-adv NÄKÖJÄÄN-ADV (NEGATE -1 HEIDÄN-PRON) ;












##! € siksi olen niin iloinen.
##! € voin tulla siksi miksi minua luulet.
SELECT:siksi-adv SIKSI-ADV (-1 BEGINSENT) (NEGATE 1 TRA) ;





SELECT:suotta-adv SUOTTA-ADV (NEGATE -1 ABE) ;



SELECT:syntyisin-adv SYNTYISIN-ADV (NEGATE -1 MINÄ-PRON) ;


SELECT:takaisin-adv TAKAISIN-ADV (NEGATE -1 MINÄ-PRON) ;


SELECT:toistamiseen-adv TOISTAMISEEN-ADV (NEGATE -1 GEN) ;










##! $ he joivat vähän vettä.
##! € hän joi sen vähän veden mitä sai.
SELECT:vähän-adv VÄHÄN-ADV (NOT 1 GEN) ;




SELECT:lujaa-adv LUJAA-ADV (NEGATE 1 PAR) ;


# suoraan = {directly, direct, straight (e.g. poker)}
SELECT:suoraan-adv SUORAAN-ADV (NEGATE -1 ILL) (NEGATE 1 ILL) ;

## !
SELECT:kapina-nom KAPINA-NOM (NEGATE -1 ESS) ;

##! $ uudessa kodissa on mukava asua
##! € kai jossain lapissa asutaan vielä kodissa
REMOVE:koti-over-kota KOTA-NOUN (0 KOTI-NOUN) ;

##! $ kuolema korjaa satoa
REMOVE:kuolema-over-kuolla KUOLEMA-VERB (0 KUOLEMA-NOUN) ;

##! $ olkaa niin hyvä ja menkää.
##! $ jokaisella on kaksi olkaa.
REMOVE:olkaa-usually-verb OLKAA-NOUN (0 OLKAA-VERB) ;

##! $ teiden päässä on Rooma.
##! € Sri Lanka on teiden valmistajamaa.
REMOVE:tee-plural-shadows-tie TEE-PLURAL (0 TIE-PLURAL) ;
##! $ teissä on sitä jotain.
##! $ monissa teissä on paljon kofeiinia.
REMOVE:tee-plural-shadows-te TEE-PLURAL (0 TE-PLURAL) ;

##! $ tilanne oli kuumottava.
##! € työntekoon soveltuvat tilanne ovat olemattomat.
REMOVE:tilanne-over-tila TILANNE-POSS (0 TILANNE-NOUN) ;

##! € vanhemmat ovat vanhempainillassa.
##! € vanhepi herrasmies.
SELECT:vahnempi-noun VANHEMPI-NOUN (NEGATE 1 NOUN) (NEGATE 2 NOUN) ;

##! € hiukan malttia on hyvä olla.
REMOVE:hiukan-adv HIUKAN-NOUN (0 ADV) ;

##! € jana on 25 mm pitkä.
SELECT:mm-measure-with-nums MM-NOUN (-1 NUM) ;

##! € käy merimies merta päin.
##! € käy merinainen merelle päin.
##! € kulkee meri-ihminen mereen päin.
SELECT:päin-adp-after-towards-case PÄIN-ADP (-1* ILL/ALL/PAR BARRIER VERB) ;

##! € on hyvä olo ollut siitä lähtien.
SELECT:lähtien-after-from-case LÄHTIEN-ADP (-1* LOCATIVE-FROM BARRIER VERB) ;



#
# selected words from original Karlsson's cg-1 end here }}}
SECTION # {{{ grammar and context

##! € en oo sitä nähny.
##! € en tule.
##! € ole hiljaa.
IFF:conneg-when-neg CONNEG (-1* NEG BARRIER CONNEG) ;

##! € poliisilla on ollut etsintäatehtävä.
##! € katoamisilmoitus on levinnyt sosiaalisessa mediassa.
SELECT:perfect-tense PCPNUT (-1* COPULA BARRIER VERBAUX) ;

##! €
SELECT:adp-gen-complements ADPS-WITH-GEN (0* GEN BARRIER ADP) ;
SELECT:adp-par-complements ADPS-WITH-PAR (0* PAR BARRIER ADP) ;
SELECT:adp-ill-complements ADPS-WITH-ILL (0* ILL BARRIER ADP) ;
SELECT:adp-ela-complements ADPS-WITH-ELA (0* ELA BARRIER ADP) ;

##! omorfi det is ~pron dem
##! €
REMOVE:det-needs-np DET (NEGATE 1 NOMINAL) ;

##! € siitä on puoli vuotta.
SELECT:nump-partitive NUM-MULTIPLIER (1 PAR) ;

##! $ vuonna 2015 kirjattiin sata viharikosta
SELECT:measurep-partitive PAR (-1 NUM) ;

##! € pääministerit tapaavat Intiassa.
SELECT:verb-pl3-agree-left VERB-PL3 (-1 NOUN + PL) ;

##! omorfi infminen is limited to cop´+ tekeminen c.f. Iso suomen kielioppi
##! $ ei ole lahjahevosta suuhin katsomineb
IFF:ei-ole-infminen INFMINEN (-1* OLE-CONNEG) ;

##! $ sanoja "lainattuja sanoja".
SELECT:final-needs-initial <POSITION_FINAL> (-2* <POSITION_INITIAL> BARRIER <POSITION_FINAL>) ;
##! $ sanoja "lainattuja sanoja".
SELECT:initial-needs-final <POSITION_INITIAL> (2* <POSITION_FINAL> BARRIER <POSITION_INITIAL>) ;

# }}}
SECTION # {{{ last-resorts

## This section is a sledgehammer for all things even slightly rare
## and suspicious

##! $ mitä jää jäljelle.
##! $ se on kerrassaan mahtavaa.
##! $ mennään suoraan sinne.
##! € aseta kortit suoraan riviin

##! € olen nähnyt tarpeeksi.

##! € olen todella viisas.
##! $ kerää tarkalleen kolme omenaa.
##! $ se on varsin totta.
##! $ joskus menee myöhään.
##! € he tanssivat myöhään yöhön.
##! $ tavallaan olet kuitenkin oikeassa.
##! € hän teki kaiken omalla tyhmällä tavallaan.
##! $ kirjoitin kaikki säännöt ihan turhaan.
##! € kulutin rahani turhaan roskaan.
##! $ panin kaikki rahani varmaan hevoseen.
##! € varmaan tuhlasin kaikki rahani siihen,
##! $ he ottivat välittömästi yhteen.
##! € panin kaikki rahani yhteen hevoseen.
##! $ yritä uudestaan.
##! $ yritä uudelleen.
##! $ voitele molemmin puolin.
##! $ he joivat vähän vettä.
##! $ he joivat paljon vettä.
##! $ he joivat enemmän vettä.
SELECT:adv-unless-adj-ine-agreement ADV-LIKE-ADJ-INE (NEGATE 1 INE) ;
SELECT:adv-unless-adj-ill-agreement ADV-LIKE-ADJ-ILL (NEGATE 1 ILL) ;
SELECT:adv-unless-adj-ins-agreement ADV-LIKE-ADJ-INS (NEGATE 1 INS) ;
SELECT:adv-unless-adj-ade-agreement ADV-LIKE-ADJ-ADE (NEGATE 1 ADE) ;
SELECT:adv-unless-adj-all-agreement ADV-LIKE-ADJ-ALL (NEGATE 1 ALL) ;
SELECT:adv-unless-adj-ela-agreement ADV-LIKE-ADJ-ELA (NEGATE 1 ELA) ;
SELECT:adv-unless-adj-gen-agreement ADV-LIKE-ADJ-GEN (NEGATE 1 GEN) ;
SELECT:adv-unless-noun-ine-agreement ADV-LIKE-NOUN-INE (NEGATE -1 INE) ;
SELECT:adv-unless-noun-tra-agreement ADV-LIKE-NOUN-TRA (NEGATE -1 TRA) ;
SELECT:adv-unless-noun-ins-agreement ADV-LIKE-NOUN-INS (NEGATE -1 INS) ;
SELECT:adv-unless-noun-ade-agreement ADV-LIKE-NOUN-ADE (NEGATE -1 ADE) ;
SELECT:adv-unless-noun-all-agreement ADV-LIKE-NOUN-ALL (NEGATE -1 ALL) ;
SELECT:adv-unless-noun-ela-agreement ADV-LIKE-NOUN-ELA (NEGATE -1 ELA) ;
##! $ ei ole lainkaan vaikeaa lakata.
##! $ sade oli kastellut hänet läpeensä.
##! $ mistä olet kotoisin?
SELECT:adverbs-with-rarer-nominals DEFAULT-ADV ;
REMOVE:adjectives-rarely-possessed ADJ + POSS ;
REMOVE:unusual-plurals NOUNS-RARELY-PLURAL ;
REMOVE:lexicalised-over-derived-sti <DRV_STI> (0 <LEX_STI>) ;
REMOVE:rare-verb-shadows-commons RARER-VERB ;
REMOVE:rare-propn-shadows-commons RARER-PROPN ;
REMOVE:rare-noun-shadows-commons RARER-NOUN ;
SELECT:prefer-punct-over-sym PUNCT (0 SYM);
REMOVE:prefer-standard NONSTANDARDS ;
REMOVE:prefer-original-case RECASED (NOT -1 BEGINSENT);
REMOVE:least-derivations DERIVATIONS ;
SELECT:least-compounds MIN-COMPOUND ;
SELECT:probably MIN-WEIGHT ;
REMOVE:avoid-instructives INS (NOT 0 VERB + INFE) ;
REMOVE:avoid-abessives ABE (NOT 0 VERB + INFMA) ;
REMOVE:avoid-clitics CLITIC ;
REMOVE:blacklists BLACKLISTED ;
REMOVE:avoid-sg0 SG0 ;
REMOVE:rare-pers-pron-forms PL (0 PERS-PRON-SG) ;
# }}}

# vim: set ft=vislcg3 foldmethod=marker:
