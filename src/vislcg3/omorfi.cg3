# omorfi.cg3
# Constraint grammar for Finnish
# GNU GPLv3
# Authors: Tommi A Pirinen
# but also remade based on Fred Karlsson's fincg:

##  This program is free software: you can redistribute it and/or modify
##  it under the terms of the GNU General Public License as published by
##  the Free Software Foundation, either version 3 of the License.
##
##  This program is distributed in the hope that it will be useful,
##  but WITHOUT ANY WARRANTY; without even the implied warranty of
##  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
##  GNU General Public License for more details.
##
##  You should have received a copy of the GNU General Public License
##  along with this program.  If not, see <http://www.gnu.org/licenses/>.

STRICT-TAGS += NOUN VERB AUX ADJ ADV ADP PROPN PRON SYM PUNCT CCONJ SCONJ INTJ NUM
    DET
    NOM PAR ACC GEN INE ELA ILL ADE ABL ALL TRA INS ABE LAT
    SG PL
    ACTV PASV INDV POTN IMPV COND
    INFA INFE INFMA
    POSSSG1 POSSSG2 POSSSP3 POSSPL1 POSSPL2
    HAN KO PA S KA KAAN KIN
    ORD CARD
    NEG CONNEG
    PCPNUT PCPVA
    SG1 SG2 SG3 PL1 PL2 PL3 SG0
    <DRV_LA> <DRV_VA> <DRV_NUT> <DRV_TU> <DRV_MA> <DRV_U> <DRV_MINEN>
    <DRV_MA> <DRV_JA> <DRV_NEN> <DRV_STI>
    <LEX_STI>
    <POSITION_FINAL> <POSITION_INITIAL>
    <STYLE_DIALECTAL> <STYLE_ARCHAIC> <STYLE_NONSTANDARD>
    <*lowercased> <*UPPERCASED> <*Titlecased> <*dETITLECASED>
    <**PROPN-BLOCKING> ;
OPTIONS += no-inline-sets ;

DELIMITERS = "<.>" "<!>" "<?>" ;

SETS

# {{{ grammar groups
## Nominal poses
LIST NOMINAL = NOUN ADJ PRON PROPN ;
## Clitic particle tags
LIST CLITIC = HAN KO PA S KA KAAN KIN ;
## Conjunctions
LIST CONJUNCTION = CCONJ SCONJ ;
## any vebs
LIST VERBAUX = VERB AUX ;
## copula?
LIST COPULA = ("olla" AUX) ;
# }}}
# {{{ sentences and clauses
LIST BEGINSENT = >>> ;
LIST ENDSENT = "<.>" "<!>" "<?>" ;
# }}}
# {{{ words

## short words that many find problems with
LIST BADEVIL_WORDS =
    ("kui" NOUN) ("tai" NOUN) ("vai" NOUN) ("pai" NOUN)
    ("par" NOUN) ("tule" NOUN) ("kuvi" NOUN) ("kos" NOUN)
    ("mon" NOUN) ("lev" NOUN)
    ("taka" NOUN) ("edus" NOUN)
    ("he" INTJ) ;

## propns that overlap commoner words
LIST RARER_PROPN =
    ("Vanha" PROPN) ("Mei" PROPN) ("Osa" PROPN) ("Hanska" PROPN)
    ("Osta" PROPN) ("Leikkaa" PROPN) ("Varo" PROPN) ("Tarkki" PROPN)
    ("Onkki" PROPN) ("Ede" PROPN) ("Mietti" PROPN) ("Metsä" PROPN)
    ("Puu" PROPN) ("Koto" PROPN) ("Mörä" PROPN) ("Tuli" PROPN)
    ("Hauska" PROPN) ("Uutinen" PROPN) ("Piha" PROPN) ("Sakko" PROPN)
    ("Ota" PROPN) ("Leikkaa" PROPN) ("Lähdet" PROPN) ("Matka" PROPN)
    ("Lähellä" PROPN) ("Makkara" PROPN) ("Maku" PROPN) ("Suo" PROPN)
    ("Kauha" PROPN) ("Tietty" PROPN) ("Muu" PROPN) ("Sen" PROPN)
    ("Erinomainen" PROPN) ("Allekirjoittanut" PROPN) ("Markkina" PROPN)
    ("Turu" PROPN) ("Muu" PROPN) ("Alku" PROPN) ("Lava" PROPN)
    ("Tila" PROPN) ("Pyhä" PROPN) ("Suora" PROPN) ("He" PROPN)
    ("Sen" PROPN) ("Ainoa" PROPN) ("Viimeinen" PROPN) ("Ole" PROPN)
    ("Jo" PROPN) ("Ole" PROPN) ("Ihana" PROPN) ("Tos" PROPN)
    ("Mies" PROPN) ("As" PROPN) ("Asi" PROPN) ("Viimeinen" PROPN)
    ("Toivo" PROPN) ("Pitkä" PROPN) ("Viimeinen" PROPN)
    ("Lusikka" PROPN) ("Asema" PROPN) ("Mies" PROPN)
    ("Olin" PROPN) ("Tieto" PROPN) ("Erä" PROPN) ("Jyrkänne" PROPN)
    ("Kello" PROPN) ("Kuppi" PROPN) ("Mutta" PROPN) ("Yhtiö" PROPN)
    ("Ka" PROPN) ("Kutsu" PROPN) ("Teos" PROPN) ("Teema" PROPN)
    ("Puute" PROPN) ("Soi" PROPN) ("Hakkuu" PROPN) ("Tu" PROPN)
    ("Vä" PROPN) ("Vähä" PROPN) ("Karta" PROPN) ("Silta" PROPN) 
    ("Onni" PROPN) ("Joki" PROPN) ("Muuri" PROPN) ("Vuo" PROPN)
    ("Vuoksi" PROPN) ("Kiva" PROPN) ("Miele" PROPN) 
    ("Maakunta" PROPN) ;
## adverbs that are rarer than adp or noun counterparts. also some
## questionable advs that should've been intjs
LIST RARER-ADV =
    ("kuin" ADV) ("kun" ADV) ;
## adps that are rarer than adv or noun counterparts
## prefer ADPS-WITH-CASE over RARER-ADP
LIST RARER-ADP =
    ("silloin" ADP) ("vaikka" ADP) ("aina" ADP) ;
## Adpositions by their complement
LIST ADPS-WITH-PAR = ("ylös" ADP) ("päin" ADP) ("vastaan" ADP) ;
LIST ADPS-WITH-GEN = ("takana" ADP) ("mukaan" ADP) ("vuoksi" ADP) 
    ("alla" ADP) ;
## Verbs with inf
LIST VERBS-WITH-INFA = ("alkaa" AUX) ("haluta" AUX) ("tahtoa" AUX) ;
LIST VERBS-WITH-INFMAAN = ("alkaa" AUX) ("päästä" AUX) ("ruveta" AUX) ;
## numerals that are used nouns too
LIST NUM-MULTIPLIER = ("puoli" NUM) ("sata" NUM) ("miljoona" NUM) ;

## the words where 2nd person singular possessives overlap with e.g. past
## 3rd person singular e.g. "voisi" = {your butter(s), he could},
## "tiesi" = {your roads, he knew} but also: "kuusi" = {your moon(s),
## six}
LIST SG2POSS-CONFUSION = ("kuu" POSSSG2) ("voi" POSSSG2) ("tie" POSSSG2)
    ("tunti" POSSSG2) ("lupa" POSSSG2) ("rupi" POSSSG2) ("hai" POSSSG2)
    ("taju" POSSSG2) ("pyy" POSSSG2) ("kerä" POSSSG2) ("vasta" POSSSG2)
    ("pää" POSSSG2) ("harja" POSSSG2) ("vuo" POSSSG2) ("pää" POSSSG2);
## "siitä" = {to conceive, from it}
LIST SIITTÄÄ-VERBS = ("siittää" VERB) ("siitä" VERB) ("si’itä" VERB) ;
LIST SIITÄ-DEM = ("se" PRON) ("se" DET) ;
## "sinun" = yours
LIST SINUN = ("sinä" SG GEN) ;
## ja ADV is spoken
LIST JA-ADV = ("ja" ADV) ;
## van Houten, etc.
LIST VAN-PROPN = ("van" PROPN) ;
## Helsingin = {Helsinki’s, Helsing’s}
LIST HELSING-PROPN = ("Helsing" PROPN) ;
## spoken
LIST ETTÄ-INTJ = ("että" INTJ) ;
## sekä - että is paired cconj, otherwise että sconj
LIST SEKÄ-CCONJ = ("sekä" CCONJ) ;
LIST ETTÄ-CCONJ = ("että" CCONJ) ;
## niin - kuin is paired cconj
LIST NIIN-CCONJ = ("niin" CCONJ) ;
LIST KUIN-CCONJ = ("kuin" CCONJ) ;
## niiden {niisi’s, theirs} (I dunno what niisi is)
LIST NIISI-NOUN = ("niisi" NOUN) ;
## ensin {first, some odd verb that doesn't really go with 1sg}
LIST ENTÄÄ-VERB = ("entää" VERB) ;
LIST ENSIN-ADV = ("ensin" ADV) ;
## nainen {female, I might marry/fuck}
LIST NAINEN-VERB = ("naida" VERB POTN) ;
## vähän = a little either adv or adj gen
LIST VÄHÄN-ADV = ("vähän" ADV) ;
LIST VÄHÄ-ADJ = ("vähä" ADJ) ;
## vaikka mitä
LIST VAIKKA-ADV = ("vaikka" ADV) ;
LIST MIKÄ-PRON = ("mikä" PRON) ;
## häntä {tail, him}
LIST HÄNTÄ-NOUN = ("häntä" NOUN) ;
LIST HÄN-PRON = ("hän" PRON) ;
# }}}
# {{{ secondaries
## we have these tags when analyser doesn't match the case
LIST RECASED = <*lowercased> <*UPPERCASED> <*Titlecased> <*dETITLECASED> ;
## we have some blacklists
LIST BLACKLISTED = <**PROPN-BLOCKING> ;
## generic weight = approximate log prob of some linear combo
LIST MIN-WEIGHT = <W=MIN> ;
## we count compound boundaries
LIST MIN-COMPOUND = <CMP=MIN> ;
## tags for known wrong or odd stuff
LIST NONSTANDARDS = <STYLE_ARCHAIC> <STYLE_DIALECTAL> <STYLE_NONSTANDARD> ;
## derivations (apart from the near-inflectional)
LIST DERIVATIONS = <DRV_LA> <DRV_U> <DRV_MINEN> <DRV_JA> <DRV_NEN>
    <DRV_ISA> <DRV_TAVA> <DRV_STI> ;


BEFORE-SECTIONS

REMOVE:bad-words BADEVIL_WORDS ;

SECTION # words minimal context

# UD preferences

# FIXME: use $$ to match lemma
REMOVE:UD-ordinal-is-adjective NUM (0 ORD) ;

# trad. possessives
# sinun {kuusi, voisi, tiesi}
REMOVE:only-poss-if-pron-sg2 SG2POSS-CONFUSION (NOT -1 SINUN) ;

# surface-form specific disambiguation between few lexemes

##! $ ollaan menos sinne ja.
REMOVE:ja-adv JA-ADV (NOT 1 ENDSENT) ;

##! $ ai että voi että
REMOVE:että-intj ETTÄ-INTJ (NOT -1 INTJ) ;
##! $ oli siellä sekä kaljaa että apinoita.
REMOVE:sekä-että-cconj ETTÄ-CCONJ (NOT -1* SEKÄ-CCONJ) ;
##! $ oli siellä niin kaljaa kuin apinoitakin.
REMOVE:niin-kuin-cconj NIIN-CCONJ (NOT 1* KUIN-CCONJ) ;

##! $ Helsingissä on lunta
##! € näitkö sen uuden van Helsingin episodin?
REMOVE:Helsing-needs-van HELSING-PROPN (NOT -1 VAN-PROPN) ;

##! € pulloissa on vanhan näköiset etiketit
REMOVE:van-needs-PROPN VAN-PROPN (NOT 1 PROPN) ;

##! € nainen on kuin morsian
REMOVE:nainen-unlikely-potential  NAINEN-VERB (0 NOUN) ;

##! € kun rahaa ei ole, löytää vaikka mitä
##! $ vaikka rahaa ei ole, löytää sitä
REMOVE:vaikka-mitä VAIKKA-ADV (NEGATE 1 MIKÄ-PRON) ;

##! € niiden pitää tulla myös
REMOVE:niisi-niiden NIISI-NOUN (0 PRON) ;

##! € siitä en tiedä mitään
##! $ siitä maailma täyteen lapsia
REMOVE:siittää-siitä SIITTÄÄ-VERBS (0 SIITÄ-DEM) ;

##! € ensin syödään sitten vasta mennään
REMOVE:entää-ensin ENTÄÄ-VERB (0 ENSIN-ADV) ;

##! € he joivat vähän vettä
SELECT:vähän-adv VÄHÄN-ADV (0 VÄHÄ-ADJ) ;

##! € he odottivat häntä
##! $ koira juoksi häntä heiluen
SELECT:häntä-pron HÄN-PRON (0 HÄNTÄ-NOUN) ;

SECTION # grammar and context

##! € en oo sitä nähny
##! € en tule
##! € ole hiljaa
REMOVE:conneg-needs-neg CONNEG (NEGATE -1* NEG BARRIER CONNEG) ;

##! € poliisilla on ollut etsintäatehtävä
##! € katoamisilmoitus on levinnyt sosiaalisessa mediassa
SELECT:perfect-tense PCPNUT (-1* COPULA BARRIER VERBAUX) ;

##! €
SELECT:adp-gen-complements ADPS-WITH-GEN (-1* GEN BARRIER ADP) (1* GEN BARRIER ADP) ;
SELECT:adp-par-complements ADPS-WITH-PAR (-1* PAR BARRIER ADP) (1* PAR BARRIER ADP) ;

##! €
REMOVE:det-needs-np DET (NEGATE 1 NOMINAL) ;

##! € siitä on puoli vuotta
SELECT:nump-partitive NUM-MULTIPLIER (1 PAR) ;

SECTION # last-resorts

SELECT:final-needs-initial <POSITION_FINAL> (-2* <POSITION_INITIAL> BARRIER <POSITION_FINAL>) ;
SELECT:initial-needs-final <POSITION_INITIAL> (2* <POSITION_FINAL> BARRIER <POSITION_INITIAL>) ;
REMOVE:lexicalised-over-derived-sti <DRV_STI> (0 <LEX_STI>) ;
REMOVE:rare-propn-shadows-commons RARER_PROPN ;
SELECT:prefer-punct-over-sym PUNCT (0 SYM);
REMOVE:prefer-standard NONSTANDARDS ;
REMOVE:prefer-original-case RECASED (NOT -1 BEGINSENT);
REMOVE:least-derivations DERIVATIONS ;
SELECT:least-compounds MIN-COMPOUND ;
SELECT:probably MIN-WEIGHT ;
REMOVE:avoid-instructives INS (NOT 0 VERB + INFE) ;
REMOVE:avoid-abessives ABE (NOT 0 VERB + INFMA) ;
REMOVE:avoid-clitics CLITIC ;
REMOVE:blacklists BLACKLISTED ;
REMOVE:avoid-sg0 SG0 ;
# vim: set ft=vislcg3:
